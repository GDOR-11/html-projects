<html>
	<head>
		<meta charset = 'UTF-8'>
		<title>
			ray marching
		</title>
		<style>
			body {
				margin:0;
			}
		</style>
		<script src = 'http://threejs.org/build/three.min.js'></script>
	</head>
	<body>
		<script id = 'codigo_glsl' type = 'codigo_glsl'>
			float min_dist = 0.01;
			float max_dist = 40.0;
			int max_passos = 250;
			float k = 20.0;
			uniform vec2 resolucao;
			uniform vec3 pos_luz;
			uniform vec3 pos_camera;
			uniform float zoom;
			uniform float tempo;
			mat3 rotx(float a) {
				return mat3(1.0, 0.0, 0.0, 0.0, cos(a), -sin(a), 0.0, sin(a), cos(a));
			}
			mat3 roty(float a) {
				return mat3(cos(a), 0.0, sin(a), 0.0, 1.0, 0.0, -sin(a), 0.0, cos(a));
			}
			mat3 rotz(float a) {
				return mat3(cos(a), -sin(a), 0.0, sin(a), cos(a), 0.0, 0.0, 0.0, 1.0);
			}
			float distancia_esfera(vec3 ponto, vec3 pos, float raio) {
				return length(pos - ponto) - raio;
			}
			float distancia_capsula(vec3 ponto, vec3 ponto_a, vec3 ponto_b, float raio) {
				return length(ponto - (ponto_a + clamp(dot(ponto - ponto_a, ponto_b - ponto_a) / dot(ponto_b - ponto_a, ponto_b - ponto_a), 0.0, 1.0) * (ponto_b - ponto_a))) - raio;
			}
			float distancia_toro(vec3 ponto, vec3 rot, vec3 s, vec3 pos, float raio1, float raio2) {
				vec3 p = (ponto - pos) * rotx(rot.x) * roty(rot.y) * rotz(rot.z) / s;
				return length(vec2(length(p.xz) - raio1, p.y)) - raio2;
			}
			float distancia_paralelepipedo(vec3 ponto, vec3 rot, vec3 pos, float altura, float largura, float profundidade) {
				vec3 p = (ponto - pos) * rotx(rot.x) * roty(rot.y) * rotz(rot.z);
				return length(vec3(max(abs(p.x) - largura / 2.0, 0.0), max(abs(p.y) - altura / 2.0, 0.0), max(abs(p.z) - profundidade / 2.0, 0.0)));
			}
			float distancia_cilindro(vec3 ponto, vec3 ponto_a, vec3 ponto_b, float raio) {
				return length(max(vec2((length(ponto - (ponto_a + dot(ponto - ponto_a, ponto_b - ponto_a) / dot(ponto_b - ponto_a, ponto_b - ponto_a) * (ponto_b - ponto_a))) - raio), (abs(dot(ponto - ponto_a, ponto_b - ponto_a) / dot(ponto_b - ponto_a, ponto_b - ponto_a) - 0.5) - 0.5) * length(ponto_b - ponto_a)), 0.0))     ;// + min(max((length(ponto - (ponto_a + dot(ponto - ponto_a, ponto_b - ponto_a) / dot(ponto_b - ponto_a, ponto_b - ponto_a) * (ponto_b - ponto_a))) - raio), (abs(dot(ponto - ponto_a, ponto_b - ponto_a) / dot(ponto_b - ponto_a, ponto_b - ponto_a) - 0.5) - 0.5) * length(ponto_b - ponto_a)), 0.0);
			}
			float distancia(vec3 ponto) {
				vec3 p = (ponto) * roty(tempo) + vec3(0.0, 0.0, 4.0);
				float dist_plano = abs(p.y);
				float dist = dist_plano;
				float dist_capsula = distancia_capsula(p, vec3(1.5, 1.0, 5.0), vec3(1.0, 2.0, 6.0), 0.25);
				dist = min(dist, dist_capsula);
				float dist_esfera = distancia_esfera(p, vec3(0.0, 1.0, 4.0), 0.75);
				dist = min(dist, dist_esfera);
				float dist_toro = distancia_toro(p, vec3(1.57079632679, 0.0, -tempo * 2.0), vec3(1.0, 1.0, 1.0), vec3(0.0, 3.0, 4.0), 1.0, 0.25);
				dist = min(dist, dist_toro);
				float dist_paralelepipedo = distancia_paralelepipedo(p, vec3(0.0, tempo, 0.0), vec3(2.0, 0.5, 3.0), 1.0, 2.0, 0.5);
				dist = min(dist, dist_paralelepipedo);
				float dist_cilindro = distancia_cilindro(p, vec3(-3, 3.0, 4.0), vec3(-2.5, 2.0, 5.0), 0.5);
				dist = min(dist, dist_cilindro);
				float dist_cubo = distancia_paralelepipedo(p, vec3(tempo, tempo, tempo), vec3(-2.0, 1.0, 3.0), 1.0, 1.0, 1.0);
				dist = min(dist, dist_cubo);
				return dist * 0.8;
			}
			float raymarch(vec3 origem_raio, vec3 dir_raio) {
				float distancia_origem = 0.0;
				for(int i = 0;i < max_passos;i++) {
					vec3 ponto_atual = origem_raio + distancia_origem * dir_raio;
					float distancia_atual = distancia(ponto_atual);
					distancia_origem += distancia_atual;
					if(distancia_atual < min_dist || distancia_origem > max_dist) {
						break;
					}
				}
				return distancia_origem;
			}
			vec3 calcula_perpendicular(vec3 ponto) {
				float dist = distancia(ponto);
				vec2 delta = vec2(0.001, 0.0);
				vec3 resultado = dist - vec3(distancia(ponto - delta.xyy), distancia(ponto - delta.yxy), distancia(ponto - delta.yyx));
				return normalize(resultado);
			}
			float iluminacao(vec3 ponto) {
				vec3 vet_luz = normalize(pos_luz - ponto);
				vec3 perpendicular = calcula_perpendicular(ponto);
				float dist = raymarch(ponto + perpendicular * min_dist * 10.0, vet_luz);
				float iluminacao = clamp(dot(perpendicular, vet_luz), 0.0, 1.0);
				if(dist < length(pos_luz - ponto)) {
					iluminacao /= 10.0;
				}
				return pow(iluminacao, 2.0);
			}
			void main() {
				vec2 pos = (gl_FragCoord.xy - resolucao.xy / 2.0) / resolucao.y;
				vec3 dir_raio = normalize(vec3(pos.x, pos.y, zoom));
				float dist = raymarch(pos_camera, dir_raio);
				vec3 ponto = pos_camera + dir_raio * dist;
				float iluminacao = iluminacao(ponto);
				gl_FragColor = vec4(iluminacao, iluminacao, iluminacao, 1.0);
			}
		</script>
		<script>
			var cena = new THREE.Scene();
			var camera = new THREE.PerspectiveCamera(90 * document.body.clientHeight / document.body.clientWidth, document.body.clientWidth / document.body.clientHeight, 0.1, 1000);
			var renderizador = new THREE.WebGLRenderer(document.body);
			renderizador.setSize(document.body.clientWidth, document.body.clientHeight);
			document.body.appendChild(renderizador.domElement);
			var relogio = new THREE.Clock();

			var cam = {
				pos: new THREE.Vector3(0, 1.5, -6),
				zoom: 1
			}
			var luz = new THREE.Vector3(0, 10, -10);

			var inputs = {};
			inputs.resolucao = {type: 'v2', value: new THREE.Vector2(document.body.clientWidth, document.body.clientHeight)};
			inputs.pos_luz = {type: 'v3', value: luz};
			inputs.pos_camera = {type: 'v3', value: cam.pos};
			inputs.olhando_para = {type: 'v3', value: cam.olhando_para};
			inputs.zoom = {type: 'f', value: cam.zoom};
			inputs.tempo = {type: 'f', value: relogio.getElapsedTime()};

			var codigo_glsl = document.getElementById('codigo_glsl').innerHTML;
			var material = new THREE.ShaderMaterial({uniforms: inputs, fragmentShader: codigo_glsl});
			var geometria = new THREE.PlaneGeometry(10, 10);
			var tela = new THREE.Mesh(geometria, material);
			cena.add(tela);
			tela.position.z--;


			function renderizar() {
				inputs.tempo.value = relogio.getElapsedTime();
				// luz = new THREE.Vector3(Math.sin(relogio.getElapsedTime()) * 5, 5, Math.cos(relogio.getElapsedTime()) * 5 + 2.5);
				// inputs.pos_luz.value = luz;
				renderizador.render(cena, camera);
				requestAnimationFrame(renderizar);
			}
			renderizar();

			// document.onmousemove = function(event) {
			// 	inputs.pos_luz.value = new THREE.Vector3((event.clientX / document.body.clientWidth - 0.5) * 5, 5, -(event.clientY / document.body.clientHeight - 0.5) * 5);
			// }
		</script>
	</body>
</html>